configfile: "config/config.yaml"

include: "wildcard_constraints.smk"
include: "rules/test_rules.smk"

rule rehead:
    input:
        "resources/gwas_pipeline/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/{input_name}.tsv.gz")
    params:
        columns_to_drop = config["extraneous_columns"],
        pan_ukb_p_column = 'pval_EUR',
        pan_ukb_neglog10_p_column = 'neglog10_pval_EUR',
        pan_ukb_beta_column = 'beta_EUR',
        pan_ukb_se_column = 'se_EUR'
    threads: 8
    script: "scripts/rehead.R"


rule fix_alleles_and_id:
    input:
        "results/gwas_pipeline/reheaded/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/{input_name}.tsv.gz")
    threads: 8
    script: "scripts/fix_alleles_and_id.R"

rule check_for_minimal_column_set:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/{input_name}.tsv.gz")
    threads: 8
    script: "scripts/check_for_minimal_column_set.R"

rule recalculate_missing_summary_statistics:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz")
    threads: 8
    script: "scripts/recalculate_missing_sumstats.R"

rule detect_build:
    input:
        manifest = "resources/gwas_pipeline/build_manifest.tsv",
        sumstats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/build_detection/{input_name}.tsv")
    threads: 8
    script: "scripts/detect_build.R"

rule create_bed_file_for_liftover:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}.bed")
    threads: 8
    script: "scripts/create_bedfile.R"

rule prepare_file_for_liftover:
    input:
        sumstats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz",
        build = "results/gwas_pipeline/build_detection/{input_name}.tsv"
    output:
        prepared = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_prepared.tsv.gz"),
        build = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_build.txt")
    threads: 8
    script: "scripts/prepare_file_for_liftover.R"

rule liftover:
    input:
        build = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_build.txt",
        bed_file = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}.bed",
        hg19_chainfile = "resources/gwas_pipeline/hg19ToHg38.over.chain.gz"
    output:
        lifted = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_lifted.bed"),
        unlifted = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_unlifted.bed")
    threads: 1
    run:
        with open(input.build, 'r') as fh:
            assembly = fh.readline().strip()

        if assembly == 'hg19':
            shell("liftOver {input.bed_file} {input.hg19_chainfile} {output.lifted} {output.unlifted}")
        elif assembly == 'hg38':
            print("Already in hg38, copying...")
            shell("cp {input.bed_file} {output.lifted}")
            shell("touch {output.unlifted}")
        else:
            raise Exception('Can only liftover from hg19 at the moment')

rule merge_liftovered_rows_with_summary_statistics:
    input:
        lifted = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_lifted.bed",
        sumstats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_prepared.tsv.gz",
        build = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_build.txt"
    threads: 8
    output:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}-hg38.tsv.gz"
    script: "scripts/merge_liftovered_rows_with_sumstats.R"
