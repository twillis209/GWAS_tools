configfile: "config/config.yaml"

def get_mem_mb(wildcards, threads):
    return threads * 3420

def get_study_specific_cols_to_drop(wildcards):
    try:
        return config['per_study_extraneous_columns'][wildcards.input_name]
    except KeyError:
        return []

include: "wildcard_constraints.smk"
include: "rules/1kGP.smk"
include: "rules/test_rules.smk"

rule rehead:
    input:
        "resources/gwas_pipeline/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/{input_name}.tsv.gz")
    params:
        columns_to_drop = config["extraneous_columns"],
        study_specific_columns_to_drop = get_study_specific_cols_to_drop,
        pan_ukb_p_column = 'pval_EUR',
        pan_ukb_beta_column = 'beta_EUR',
        pan_ukb_se_column = 'se_EUR'
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/rehead.R"

rule fix_alleles_and_id:
    input:
        "results/gwas_pipeline/reheaded/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/{input_name}.tsv.gz")
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/fix_alleles_and_id.R"

rule check_for_minimal_column_set:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/{input_name}.tsv.gz")
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/check_for_minimal_column_set.R"

rule recalculate_missing_summary_statistics:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz")
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/recalculate_missing_sumstats.R"

rule tabulate_allele_switches_using_1kG:
    input:
        sum_stats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz",
        legend = "results/1kG/hg19/combined.legend.gz"
    output:
        merged_stats_with_code = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}_with_allele_codes.tsv.gz"),
        table = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}_table.tsv.gz"
    params:
        chr_col = 'CHR19',
        bp_col = 'BP19'
    threads: 16
    resources:
        runtime = 45
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/tabulate_allele_switches_using_1kG.R"

rule detect_build:
    input:
        manifest = "resources/gwas_pipeline/build_manifest.tsv",
        sumstats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/build_detection/{input_name}.tsv")
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/detect_build.R"

rule create_bed_file_for_liftover:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz"
    output:
        temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}.bed")
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/create_bedfile.R"

rule prepare_file_for_liftover:
    input:
        sumstats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/{input_name}.tsv.gz",
        build = "results/gwas_pipeline/build_detection/{input_name}.tsv"
    output:
        prepared = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_prepared.tsv.gz"),
        build = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_build.txt")
    threads: 8
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/prepare_file_for_liftover.R"

rule liftover:
    input:
        build = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_build.txt",
        bed_file = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}.bed",
        hg19_chainfile = "resources/gwas_pipeline/hg19ToHg38.over.chain.gz"
    output:
        lifted = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_lifted.bed"),
        unlifted = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_unlifted.bed")
    threads: 1
    group: "gwas_tools"
    run:
        with open(input.build, 'r') as fh:
            assembly = fh.readline().strip()

        if assembly == 'hg19':
            shell("liftOver {input.bed_file} {input.hg19_chainfile} {output.lifted} {output.unlifted}")
        elif assembly == 'hg38':
            print("Already in hg38, copying...")
            shell("cp {input.bed_file} {output.lifted}")
            shell("touch {output.unlifted}")
        else:
            raise Exception('Can only liftover from hg19 at the moment')

rule merge_liftovered_rows_with_summary_statistics:
    input:
        lifted = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_lifted.bed",
        sumstats = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_prepared.tsv.gz",
        build = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}_build.txt"
    threads: 8
    output:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}-hg38.tsv.gz"
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/merge_liftovered_rows_with_sumstats.R"

use rule tabulate_allele_switches_using_1kG as tabulate_allele_switches_using_hg38_1kG with:
    input:
        sum_stats ="results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/{input_name}-hg38.tsv.gz",
        legend = "results/1kG/hg38/combined.legend.gz"
    output:
        merged_stats_with_code = temp("results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/realigned_alleles/{input_name}_with_allele_codes.tsv.gz"),
        table = "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/realigned_alleles/{input_name}_table.tsv.gz"
    params:
        chr_col = 'CHR38',
        bp_col = 'BP38'

rule align_alleles_to_1kG:
    input:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/realigned_alleles/{input_name}_with_allele_codes.tsv.gz"
    output:
        "results/gwas_pipeline/reheaded/fixed_alleles/min_col_set/recalc_sumstats/liftover/realigned_alleles/{input_name}.tsv.gz"
    params:
        strand_policy = lambda w: config['strand_policy'][w.input_name]
    threads: 16
    resources:
        runtime = 20
    conda: "envs/gwas_tools.yaml"
    group: "gwas_tools"
    script: "scripts/align_alleles_to_1kG.R"
